// Knights Province dynamic script.
// Wiki available at: https://github.com/Kromster80/knights_province/wiki

const
  HUMAN_PLAYER = 0;
  CIDER_NEEDED = 65;
  CIDER_PACKAGE = 10;
  FISH_PACKAGE = 20;
  ownCampX = 18;
  ownCampY = 90;
  allyCampUnloadX = 101;
  allyCampUnloadY = 68;

var
  ownCamp: Integer;
  ciderDealShown: Boolean;

procedure OnMissionStart;
begin
  ownCamp := States.HouseAt(ownCampX, ownCampY);

  Actions.PlayerMessage(HUMAN_PLAYER, '<$2>', True);
end;


procedure OnScriptButtonClick(aPlayer, aTag: Integer);
var
  I: Integer;
  wagon: Integer;
begin
  if (aPlayer = HUMAN_PLAYER) and (aTag = 1) then
    if States.HouseWareInside(ownCamp, wtCider) >= CIDER_PACKAGE then
    begin
      // Create a wagon
      for I := -2 to 8 do
      begin
        wagon := Actions.GiveUnit(HUMAN_PLAYER, utWagon, ownCampX + I, ownCampY + 1, 1);
        if wagon <> 0 then
        begin
          Actions.HouseWareRemove(ownCamp, wtCider, CIDER_PACKAGE);
          Actions.UnitCarryGive(wagon, wtCider, CIDER_PACKAGE);
          Actions.UnitOrderWalk(wagon, allyCampUnloadX, allyCampUnloadY);
          Break;
        end;
      end;    
    end;
end;


procedure OnTerrainTrigger(aTriggerId: Integer; aUnit: Integer; aX, aY: Word);
var
  I: Integer;
  wagon: Integer;
begin
  if (aTriggerId = 1) and (States.UnitType(aUnit) = utWagon) and (States.UnitCarryType(aUnit) = wtCider)  then
  begin
    // Destroy wagon
    Actions.UnitKill(aUnit, True);
  
    // Create new wagon
    for I := -2 to 2 do
    begin
      wagon := Actions.GiveUnit(HUMAN_PLAYER, utWagon, allyCampUnloadX + I, allyCampUnloadY - 5, 1);
      if wagon <> 0 then
      begin
        Actions.UnitCarryGive(wagon, wtFish, FISH_PACKAGE);
        Actions.UnitOrderWalk(wagon, ownCampX, ownCampY);
        Break;
      end;
    end;
  end;
end;


procedure OnTick(aTick: Integer);
begin
  if aTick mod 50 = 0 then
  if not ciderDealShown and (States.StatWaresBalance(HUMAN_PLAYER, wtCider) >= CIDER_NEEDED + CIDER_PACKAGE) then
  begin
    Actions.PlayerMessageWithButtons(HUMAN_PLAYER, '<$4>', ['Yes', 'No'], ['', ''], [1,2], True, False, False);
    ciderDealShown := True;
  end;
end;


procedure OnPlayerVictory(aPlayer: Integer);
begin
  CampaignData.Won[1] := True;
end;