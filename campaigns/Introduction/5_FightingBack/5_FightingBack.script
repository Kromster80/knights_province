// Knights Province dynamic script.
// Wiki available at: https://github.com/Kromster80/knights_province/wiki

const
  HUMAN_PLAYER = 0;
  WOOD_IN_WAGON = 50;

var
  objWood, objSword: Integer;
  swordDead: Boolean;
  camp: Integer;
  swordsmanUnit, swordsmanGroup: Integer;

procedure UpdateObjectives;
begin
  if (objSword <> -1) and swordDead then
  begin
    Actions.PlayerObjectiveUpdateStatus(HUMAN_PLAYER, objSword, osCompleted);
    objSword := -1;
  end;

  // Compare with wagon value, since wood can also be obtained from destroyed houses
  if (objWood <> -1) and (States.HouseWareInside(camp, wtWood) >= WOOD_IN_WAGON) then
  begin
    Actions.PlayerObjectiveUpdateStatus(HUMAN_PLAYER, objWood, osCompleted);
    objWood := -1;
  end;

  if (objSword = -1) and (objWood = -1) then
    Actions.PlayerWin([HUMAN_PLAYER], True);
end;

procedure OnUnitDied(aUnit: Integer; aKiller: Integer);
begin
  if aUnit = swordsmanUnit then
    swordDead := True;
end;

procedure OnMissionStart;
begin
  swordDead := False;
  swordsmanUnit := States.UnitAt(64, 23);
  swordsmanGroup := States.GroupAt(64, 23);
  camp := States.HouseAt(20, 35);

  objWood  := Actions.PlayerObjectiveAdd(HUMAN_PLAYER, otMain, 0, osActive, '<$2>');
  objSword := Actions.PlayerObjectiveAdd(HUMAN_PLAYER, otMain, 1, osActive, '<$3>');

  UpdateObjectives;
end;

procedure OnTick(aTick: Integer);
begin
  if aTick mod 10 = 0 then
    UpdateObjectives;

  // Ensure to stay in place (and not chase Woodcutters and die of hunger)
  if aTick mod 40 = 0 then
    Actions.GroupOrderWalk(swordsmanGroup, 64, 23, 4);
end;